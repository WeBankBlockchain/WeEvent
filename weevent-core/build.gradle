apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "signing"

dependencies {
    // web3sdk for FISCO-BCOS 2.0
    implementation("org.fisco-bcos.java-sdk:java-sdk:${web3sdkVersion.fisco}") {
        // io.netty:netty-tcnative-boringssl-static:2.0.20.Final crash
        // exclude group: "io.netty"
        exclude group: "io.netty", module: "netty-tcnative-boringssl-static"
        // compile solidity
        exclude group: "org.ethereum", module: "solcJ-all"
        exclude group: "org.slf4j", module: "slf4j-log4j12"
    }

    implementation("org.springframework:spring-beans:${springVersion}")
    implementation("org.springframework:spring-context:${springVersion}")

    // fabric 1.4.4
    implementation("org.hyperledger.fabric-sdk-java:fabric-sdk-java:${fabricVersion}") {
        exclude group: "io.netty", module: "netty-tcnative-boringssl-static"
    }

    // depend project
    api project(":weevent-client")

    // log for test
    testImplementation("org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}")
    
    // spring boot
    implementation("org.springframework.boot:spring-boot-starter")
    implementation("org.springframework.boot:spring-boot-starter-web")

    implementation("com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-discovery:${nacosVersion}")
    implementation("com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-config:${nacosVersion}")
    
    testImplementation("org.springframework.boot:spring-boot-starter-test")
}

jar {
    destinationDir file("dist/apps")
    archiveName project.name + "-" + version + ".jar"

    exclude "**/*.xml"
    exclude "**/*.properties"

    doLast {
        copy {
            from file("src/main/resources/")
            into "dist/conf"
        }
        copy {
            from configurations.runtime
            into "dist/lib"
        }
        copy {
            from file("build/libs/")
            into "dist/apps"
        }
    }
}

// for upload to maven
task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}
