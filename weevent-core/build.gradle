apply plugin: "io.spring.dependency-management"
apply plugin: "signing"

dependencies {
    // web3sdk for FISCO-BCOS 2.0
    implementation("org.fisco-bcos:web3sdk:${web3sdkVersion.fiscoV2}") {
        // conflict with io.netty:netty-all:4.1.15.Final in web3sdk 1.3
        // io.netty:netty-tcnative-boringssl-static:2.0.20.Final crash
        exclude group: "io.netty"
        // compile solidity
        exclude group: "org.ethereum", module: "solcJ-all"
    }

    // web3sdk for FISCO-BCOS 1.3
    implementation("org.fisco-bcos:web3sdk-weevent:${web3sdkVersion.fiscoV1}") {
        exclude group: "org.slf4j", module: "slf4j-log4j12"
        exclude group: "org.springframework"
    }

    // fabric 1.4.4
    implementation("org.hyperledger.fabric-sdk-java:fabric-sdk-java:${fabricVersion}")

    // depend project
    implementation(project(":weevent-client"))

    // log for test
    testImplementation("org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}")
}

jar {
    destinationDir file("dist/apps")
    archiveName project.name + "-" + version + ".jar"

    exclude "**/*.xml"
    exclude "**/*.properties"

    doLast {
        copy {
            from file("src/main/resources/")
            into "dist/conf"
        }
        copy {
            from configurations.runtime
            into "dist/lib"
        }
        copy {
            from file("build/libs/")
            into "dist/apps"
        }
    }
}

// for upload to maven
task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}
