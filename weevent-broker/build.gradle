apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "com.gorylenko.gradle-git-properties"

dependencies {
    // spring boot
    implementation("org.springframework.boot:spring-boot-starter")
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.data:spring-data-commons:2.2.0.RELEASE")

    // spring boot with log4j2
    implementation("org.springframework.boot:spring-boot-starter-log4j2")
    // avoid "Log4j appears to be running in a Servlet environment, but there's no log4j-web module available."
    implementation("org.apache.logging.log4j:log4j-web:2.11.2")

    // restful and json rpc support http base authorization
    implementation("org.springframework.boot:spring-boot-starter-security")

    // stomp
    implementation("org.springframework.boot:spring-boot-starter-websocket")

    // mqtt
    implementation("org.springframework.integration:spring-integration-mqtt")

    // json rpc, https://mvnrepository.com/artifact/com.github.briandilley.jsonrpc4j/jsonrpc4j
    implementation("com.github.briandilley.jsonrpc4j:jsonrpc4j:1.5.3")

    // zookeeper 3.5.5 is first stable version support netty 4.1
    implementation("org.apache.zookeeper:zookeeper:3.5.5") {
        exclude group: "org.slf4j", module: "slf4j-log4j12"
    }
    // curator prefer zookeeper 3.5
    // http://curator.apache.org/zk-compatibility.html
    implementation("org.apache.curator:curator-recipes:4.2.0")

    // redis
    implementation("redis.clients:jedis:3.0.0")

    // web3sdk for FISCO-BCOS 2.0
    implementation("org.fisco-bcos:web3sdk:2.1.1") {
        // conflict with io.netty:netty-all:4.1.15.Final in web3sdk 1.3
        // io.netty:netty-tcnative-boringssl-static:2.0.20.Final crash
        exclude group: "io.netty"
        // compile solidity
        exclude group: "org.ethereum", module: "solcJ-all"
    }
    // web3sdk for FISCO-BCOS 1.3
    implementation("org.fisco-bcos:web3sdk-weevent:1.2.5") {
        exclude group: "org.slf4j", module: "slf4j-log4j12"
        exclude group: "org.springframework"
    }

    // fabric 1.4.4
    implementation("org.hyperledger.fabric-sdk-java:fabric-sdk-java:1.4.4")

    // depend project
    implementation project(path: ":weevent-client")

    testImplementation("org.springframework.boot:spring-boot-starter-test")
    // JMS sample
    testImplementation("javax.jms:jms:1.1")
    // mock
    testImplementation("org.jmockit:jmockit:1.9")
}

gitProperties {
    dateFormat = "yyyy-MM-dd HH:mm:ss"
    dateFormatTimeZone = "GMT+8"
    gitPropertiesDir = "${project.rootDir}/weevent-broker/src/main/resources"
}

bootJar {
    destinationDir file("dist/apps")
    archiveName project.name + "-" + version + ".jar"

    // thin spring boot jar
    excludes = ["*.jar"]

    doLast {
        copy {
            from file("src/main/resources/")
            into "dist/conf"
        }
        copy {
            from configurations.runtimeClasspath
            into "dist/lib"
        }
    }
}

bootJar.dependsOn(generateGitProperties)
