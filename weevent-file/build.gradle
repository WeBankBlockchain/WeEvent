apply plugin: "io.spring.dependency-management"

dependencies {
    testImplementation("org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}")
    // avoid "Log4j appears to be running in a Servlet environment, but there's no log4j-web module available."
    implementation("org.apache.logging.log4j:log4j-web:${log4jVersion}")

    // web3sdk for FISCO-BCOS 2.0
    implementation("org.fisco-bcos.java-sdk:java-sdk:${web3sdkVersion.fisco}") {
        // io.netty:netty-tcnative-boringssl-static:2.0.20.Final crash
        exclude group: "io.netty"
        // compile solidity
        exclude group: "org.ethereum", module: "solcJ-all"
    }

    implementation("org.springframework:spring-context:${springVersion}")

    // depend project
    api project(":weevent-core")

    implementation("commons-net:commons-net:${commonsNetVersion}")

    implementation("org.bouncycastle:bcprov-jdk15on:${bouncycastleVersion}")
    implementation("org.bouncycastle:bcpkix-jdk15on:${bouncycastleVersion}")
    implementation("org.bouncycastle:bcmail-jdk15on:${bouncycastleVersion}")

    implementation("org.mockito:mockito-core:${jmockVersion}")
}


jar {
    destinationDir file("dist/apps")
    archiveName project.name + "-" + version + ".jar"

    exclude "**/*.xml"
    exclude "**/*.properties"

    doLast {
        copy {
            from file("src/main/resources/")
            into "dist/conf"
        }
        copy {
            from configurations.runtime
            into "dist/lib"
        }
        copy {
            from file("build/libs/")
            into "dist/apps"
        }
    }
}

// for upload to maven
task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}