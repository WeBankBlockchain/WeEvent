group 'weevent-client'
version = "0.9.0"
apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = 1.8
[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'
repositories {
    maven {
        url "http://maven.aliyun.com/nexus/content/groups/public/"
    }
    mavenLocal()
    mavenCentral()
}

List lombok = [
        "org.projectlombok:lombok:1.16.14"
]

List apache_commons = [
        "org.apache.commons:commons-collections4:4.1",
        "org.apache.commons:commons-lang3:3.5",
]

List client_depends = [
        "com.github.briandilley.jsonrpc4j:jsonrpc4j:1.5.3",
        "javax.jms:jms:1.1",
        "org.java-websocket:Java-WebSocket:1.4.0",
        "org.springframework:spring-messaging"
]

dependencies {
    compile lombok, apache_commons, client_depends
    runtime lombok, apache_commons, client_depends
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

sourceSets {
    main {
        java {
            srcDir '../src/main/java/com/webank/weevent/sdk'
        }
        resources {
            srcDir '../src/main/resources'
        }
    }
}

jar {
    from(sourceSets.main.output) {
        include "com/webank/weevent/sdk/**"
    }

    destinationDir file('dist/apps')
    archiveName project.name + '-' + version + '.jar'
    exclude '**/*.xml'
    exclude '**/*.properties'

    doLast {
        copy {
            from file('../src/main/resources/')
            into 'dist/conf'
        }
        copy {
            from configurations.runtime
            into 'dist/lib'
        }
        copy {
            from file('.').listFiles().findAll { File f -> (f.name.endsWith('.bat') || f.name.endsWith('.sh') || f.name.endsWith('.env')) }
            into 'dist'
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: 'http://maven.weoa.com/nexus/content/repositories/releases/') {
                authentication(userName: 'deployment', password: 'deployment123')
            }
            pom.groupId = 'com.webank.weeventclient.test'
            pom.version = '1.0'
            pom.artifactId = 'weevent-client'
        }
    }
}