apply from: "${project.rootDir}/config.gradle"
apply plugin: "io.spring.dependency-management"
apply plugin: "signing"

dependencies {
    // json rpc
    implementation("com.github.briandilley.jsonrpc4j:jsonrpc4j:${jsonrpc4j2Version}")

    // jms
    implementation("javax.jms:jms:${jms2Version}")

    // stomp, see https://github.com/TooTallNate/Java-WebSocket
    implementation("org.java-websocket:Java-WebSocket:${WebSocket2Version}")
    implementation("org.springframework.data:spring-data-commons:${springDataCommons2Version}")
    implementation("org.springframework:spring-messaging:${spring2Version.messaging}")

    // event-client.log for test
    testImplementation("org.apache.logging.log4j:log4j-slf4j-impl:${logging2Version.log4j_slf4j_impl}")
}

jar {
    destinationDir file("dist/apps")
    archiveName project.name + "-" + version + ".jar"

    exclude "**/*.xml"
    exclude "**/*.properties"

    doLast {
        copy {
            from file("src/main/resources/")
            into "dist/conf"
        }
        copy {
            from configurations.runtime
            into "dist/lib"
        }
        copy {
            from file("build/libs/")
            into "dist/apps"
        }
    }
}

// for upload to maven
task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}
