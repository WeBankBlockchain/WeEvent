buildscript {
    repositories {
        maven {
            url "http://maven.aliyun.com/nexus/content/groups/public/"
        }
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.1.0.RELEASE")
    }
}

allprojects {
    group "com.webank.weevent"
    version "1.0.0"

    apply plugin: "java"
    apply plugin: "maven"
    apply plugin: "idea"
    apply plugin: "eclipse"
    apply plugin: "jacoco"

    // jdk version
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
        maven { url "https://dl.bintray.com/ethereum/maven/" }
        mavenLocal()
        mavenCentral()
    }

    // encoding type
    [compileJava, compileTestJava, javadoc]*.options*.encoding = "UTF-8"

    sourceSets {
        main {
            java {
                srcDirs = ["src/main/java"]
            }
            resources {
                srcDirs = ["src/main/resources"]
            }
        }
        test {
            java {
                srcDirs = ["src/test/java"]
            }
            resources {
                srcDirs = ["src/test/resources"]
            }
        }
    }

    configurations.all {
        // exclude spring-boot's default logger logback
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"

        resolutionStrategy {
            // fix security bug
            force "com.alibaba:fastjson:1.2.58"
        }
    }

    // all project common lib
    dependencies {
        implementation("org.projectlombok:lombok:1.16.14")

        // apache commons
        implementation("org.apache.commons:commons-lang3:3.6")
        implementation("org.apache.commons:commons-collections4:4.1")

        // self4j logger
        implementation("org.slf4j:slf4j-api:1.7.26")

        // junit test
        testImplementation("junit:junit:4.12")
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport
