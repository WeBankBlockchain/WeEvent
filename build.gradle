apply plugin: 'java'
apply plugin: 'jacoco'

buildscript {
    repositories {
        maven {
            url "http://maven.aliyun.com/nexus/content/groups/public/"
        }
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.1.0.RELEASE")
    }
}
allprojects {
    group 'com.webank.weevent'
    version '0.9.0'

    apply plugin: 'idea'
    apply plugin: "maven"

    repositories {
        maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
        maven { url "https://dl.bintray.com/ethereum/maven/" }
        mavenLocal()
        mavenCentral()
    }
}

subprojects {

    apply plugin: 'java'
    // jdk version
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    // encoding type
    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    List lombok = [
            "org.projectlombok:lombok:1.16.14"
    ]

    //web3sdk contains logger handler org.slf4j:slf4j-log4j12:1.7.25
    List logger = [
            "org.apache.logging.log4j:log4j-api:2.11.2",
            "org.apache.logging.log4j:log4j-core:2.11.2",
            "org.apache.logging.log4j:log4j-web:2.11.2",
            "org.slf4j:slf4j-api:1.7.26",
            "org.apache.logging.log4j:log4j-slf4j-impl:2.11.2"
    ]

    List apache_commons = [
            "org.apache.commons:commons-lang3:3.6",
            "org.apache.commons:commons-collections4:4.1"
    ]

    dependencies {
        implementation(lombok, apache_commons, logger)
    }
}

task createWrapper(type: Wrapper) {
    gradleVersion = '4.8'
}


jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport


