group = "com.webank.weevent"
version = "0.9.0"

apply plugin: "java"
apply plugin: "maven"
apply plugin: "idea"
apply plugin: "eclipse"
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
springBoot {
    buildInfo()
}
buildscript {
    repositories {
        maven {
            url "http://maven.aliyun.com/nexus/content/groups/public/"
        }
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.1.0.RELEASE")
    }
}

allprojects {
    repositories {
        maven {
            url "http://maven.aliyun.com/nexus/content/groups/public/"
        }
        mavenLocal()
        mavenCentral()
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
[compileJava, compileTestJava, javadoc]*.options*.encoding = "UTF-8"
compileJava {
    options.compilerArgs << "-parameters"
}

List lombok = [
        "org.projectlombok:lombok:1.16.14"
]

configurations {
    all*.exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
}

//web3sdk contains logger handler org.slf4j:slf4j-log4j12:1.7.25
List logger = [
        "org.apache.logging.log4j:log4j-api:2.11.2",
        "org.apache.logging.log4j:log4j-core:2.11.2",
        "org.apache.logging.log4j:log4j-web:2.11.2",
        "org.slf4j:slf4j-api:1.7.26",
        "org.apache.logging.log4j:log4j-slf4j-impl:2.11.2"
]

List apache_commons = [
        "org.apache.commons:commons-lang3:3.6",
        "org.apache.commons:commons-dbcp2:2.6.0"
]


dependencies {
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.mybatis.spring.boot:mybatis-spring-boot-starter:1.3.2")
    implementation("org.mariadb.jdbc:mariadb-java-client")
    implementation("org.apache.httpcomponents:httpclient:4.5.2")
    implementation("org.influxdb:influxdb-java:2.14")
    implementation("org.jsoup:jsoup:1.9.2")
    implementation("com.alibaba:fastjson:1.2.4")

    implementation(lombok, logger, apache_commons)

    testImplementation("org.springframework.boot:spring-boot-starter-test")
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java"]
        }
        resources {
            srcDirs = ["src/main/resources"]
        }
    }
    test {
        java {
            srcDirs = ["src/test/java"]
        }
        resources {
            srcDirs = ["src/test/resources"]
        }
    }
}

bootJar {
    destinationDir file('dist/apps')
    archiveName 'weevent-governance-' + version + '.jar'
    exclude '**/*.xml'
    exclude '**/*.properties'

    copy {
        from file('src/main/resources/')
        into 'dist/conf'
    }
    copy {
        from file('web/html/')
        into 'dist/html'
    }
    copy {
        from configurations.runtime
        into 'dist/lib'
    }
    copy {
        from file('.').listFiles().findAll { File f -> (f.name.endsWith('.sql') || f.name.endsWith('.sh') || f.name.endsWith('.env')) }
        into 'dist'
    }
}
